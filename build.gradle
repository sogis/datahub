
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.apache.cayenne.plugins:cayenne-gradle-plugin:4.2'
        classpath 'org.postgresql:postgresql:42.6.0'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
}

apply plugin: 'org.apache.cayenne'
apply from: "$rootDir/gradle/versioning.gradle"

group = 'ch.so.agi.datahub'

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

compileJava {
    options.compilerArgs.addAll(['--release', '21'])
}

bootJar {
   archiveFileName = archiveBaseName.get() + "-" + version + "-exec.jar"
}

repositories {
    mavenCentral()
    maven { url 'https://jars.interlis.ch/' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-ldap'
    implementation 'org.springframework.boot:spring-boot-starter-web' 
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'  
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    
    implementation platform('org.joinfaces:joinfaces-platform:5.2.4')
    implementation 'org.joinfaces:faces-spring-boot-starter'
    implementation 'org.joinfaces:primefaces-spring-boot-starter'
    implementation 'org.webjars.npm:primeflex:3.3.1'
    
    implementation 'org.jobrunr:jobrunr-spring-boot-3-starter:6.3.4'
    
    implementation 'ch.interlis:ilivalidator:1.14.2-SNAPSHOT'

    implementation 'org.apache.cayenne:cayenne-server:4.2'

    runtimeOnly 'org.postgresql:postgresql'
    
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

cayenne.defaultDataMap "$projectDir/src/main/resources/cayenne/datamap.map.xml"

cgen {
    client = false
    mode = 'entity'
    overwrite = true
    //createPKProperties = true
}

cdbimport {
    map "$projectDir/src/main/resources/cayenne/datamap.map.xml"
    
    cayenneProject "$projectDir/src/main/resources/cayenne/cayenne-project.xml"
    
    dataSource {
        driver 'org.postgresql.Driver'
        url "jdbc:postgresql://localhost:54321/edit"
        username 'postgres'
        password 'secret'
    }

    dbImport {
        usePrimitives false
        defaultPackage 'ch.so.agi.datahub.cayenne'
            schema {
                name 'agi_datahub_config_v1'
                includeTable 'core_operat'
                includeTable 'core_organisation'
                includeTable 'core_apikey'
                includeTable 'core_theme'
                includeTable 'core_user'
                includeTable 'deliveries_delivery'
                includeTable 'deliveries_asset'
            }
            schema {
                name 'agi_datahub_log_v1'
                includeTable 'deliveries_delivery'
                includeTable 'v_jobresponse'
            }
    }
}

